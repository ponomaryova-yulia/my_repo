=======================================================================
     CONTROL:  EDIT Deliver To
     EVENT:  Control Exited/Changed-Inline
-----------------------------------------------------------------------
     OPT: Using Defaults
0001 // **************************************************************
0002 // GSI Begin
0003 // GSI/Scott Beebe 11-25-13
0004 // **************************************************************
0005 Press Button(FC Set Header Dirty)
0006 // **************************************************************
0007 // GSI End
0008 // **************************************************************

=======================================================================
     CONTROL:  EDIT Paid By
     EVENT:  Control Exited/Changed-Inline
-----------------------------------------------------------------------
     OPT: Using Defaults
0001 // **************************************************************
0002 // GSI Begin
0003 // GSI/Scott Beebe 11-25-13
0004 // **************************************************************
0005 Press Button(FC Set Header Dirty)
0006 //
0007 // Get paid to name
0008 F0101.Fetch Single
        FC Paid By =  TK Address Number
        FC PAID BY <- TK Name - Alpha
0009 // **************************************************************
0010 // GSI End
0011 // ************************************************************** 


=======================================================================
     CONTROL:  BUTTON E&dit Full Header
     EVENT:  Button Clicked
-----------------------------------------------------------------------
     evt_cCancelClicked
     evt_nHdrChangesPushedToLines
     evt_cReprocessHeader
     OPT: Using Defaults
0001 //
0002 //
0003 // *************************************************************
0004 // GSI Begin
0005 // GSI/Scott Beebe  12-2-13
0006 // *************************************************************
0007 // Save the custom fields to cache before calling the Edit Header form.
0008 Call Function(<Current Subform>, FC AbcSaveCustomDataToCache)
0009 // *************************************************************
0010 // GSI End
0011 // *************************************************************
0012 Press Button(FC Process Header)
0013 // Make sure header has been processed successfully before exiting to the
0014 // Header Revisions form
0015 //
0016 If VA frm_cHeaderProcessStatus is equal to VA frm_cHEADER_STATUS_SUCCESS Or VA frm_cOrderMode is not equal to "U"
0017 !    Call( App:P42101 , Form:W42101E , Ver: SI szViewVersion )
     !       SI szSessionKey -> FI szSessionKey
     !       VA evt_cCancelClicked <- FI cCancelClicked
     !       VA evt_nHdrChangesPushedToLines <- FI nHdrChangesPushedToLines
0018    Call( App:P5542101 , Form:W5542101E , Ver: SI szViewVersion )
           SI szSessionKey -> FI szSessionKey
           VA evt_cCancelClicked <- FI cCancelClicked
           VA evt_nHdrChangesPushedToLines <- FI nHdrChangesPushedToLines
0019    If VA evt_cCancelClicked is not equal to "1"
0020       // Editing header is allowed in Add or Copy mode to clear errors caused by
0021       // fields not displayed in P421007.  If this is done, the header must be
0022       // reprocessed when coming back to clear any previous errors.
0023       If VA frm_cHeaderProcessStatus is not equal to VA frm_cHEADER_STATUS_SUCCESS
0024          VA evt_cReprocessHeader = "1"
0025       End If
0026       Press Button(FC Refresh Header)
0027       If VA evt_cReprocessHeader is equal to "1"
0028          Press Button(FC Process Header)
0029       End If
0030       //
0031       If VA evt_nHdrChangesPushedToLines is equal to "1"
0032          SI nMessageType = VA frm_nMSG_HDR_TO_DETAIL_UPDATE
0033          Press Button(FC Send Message)
0034       End If
0035       //
0036       SI nMessageType = VA frm_nMSG_HEADER_PROCESSED

0037       Press Button(FC Send Message)
0038       //
0039       // *************************************************************
0040       // GSI Begin
0041       // GSI/Scott Beebe  12-2-13
0042       // *************************************************************
0043       // Get the custom values from cache and assign to FC fields.
0044       VA frm_KeyName_DL010 = "DelToPaidTo"
0045       Retreive Keyed Cache Value
              SI szSessionKey -> BF szSessionKey
              VA frm_KeyName_DL010 -> BF szKey1
              VA frm_Value1_DL010 <- BF szValue1
              VA frm_Value2_DL010 <- BF szValue2
0046       If VA frm_Value1_DL010 is greater than <Blank>
0047          FC Deliver To = [VA frm_Value1_DL010]
0048       End If
0049       If VA frm_Value2_DL010 is greater than <Blank>
0050          FC Paid By = [VA frm_Value2_DL010]
0051          // Get paid to name
0052          F0101.Fetch Single
                 FC Paid By =  TK Address Number
                 FC PAID BY <- TK Name - Alpha
0053       End If
0054       // *************************************************************
0055       // GSI End
0056       // *************************************************************
0057    End If
0058 End If
0059 // *************************************************************
0060 // GSI Begin
0061 // GSI/Scott Beebe  12-2-13
0062 // *************************************************************
0063 // Delete the custom cache
0064 ! Delete Keyed Cache
     !    SI szSessionKey -> BF szSessionKey
0065 // *************************************************************
0066 // GSI End
0067 // ************************************************************* 
=======================================================================
     CONTROL:  BUTTON Process Header [HIDDEN]
     EVENT:  Button Clicked
-----------------------------------------------------------------------
     evt_nIsRefreshRequired
     OPT: Using Defaults
0001 If VA frm_cHeaderDirty is equal to "1"
0002    // *************************************************************
0003    // GSI Begin
0004    // GSI/Scott Beebe  1-8-14
0005    // *************************************************************
0006    // Save the custom fields to cache before calling the Edit Header form.
0007    Call Function(<Current Subform>, FC AbcSaveCustomDataToCache)
0008    // *************************************************************
0009    // GSI End
0010    // *************************************************************
0011    //
0012    VA frm_nSalesOrderAction = VA frm_nPROCESS_SO_HEADER
0013    // SAR 8955606 - Taxed Prices Enhancement
0014    If SV Form_ Mode is equal to CO COPY_MODE
0015       VA frm_cUseTaxedPrices = FC Use Taxed Prices
0016    End If
0017    // SAR 8955606 - Map Use Taxed Prices Variable
0018    S421007A Sales Order Header View Controller 0019    //
0020    // SAR 8955606 - Taxed Prices Enhancement
0021    FC Use Taxed Prices = VA frm_cUseTaxedPrices
0022    //
0023    VA frm_nSalesOrderAction = "0"
0024    //
0025    Press Button(FC Set Previous Field Values)
0026    //
0027    If VA frm_nViewSelectionRequired is equal to "1"
0028       Dispatch Next View
              SI szSessionKey -> BF szSessionKey
              <Zero> -> BF mnLineKey
              VA evt_nIsRefreshRequired <- BF nIsRefreshRequired
0029    End If
0030    //
0031    If VA evt_nIsRefreshRequired is equal to "1"
0032       SI nMessageType = VA frm_nMSG_TEMPLATE_ITEM_SELECTD
0033       Press Button(FC Send Message)
0034    End If
0035    //
0036    If VA frm_cHeaderProcessStatus is equal to VA frm_cHEADER_STATUS_SUCCESS
0037       SI nMessageType = VA frm_nMSG_HEADER_PROCESSED
0038       Press Button(FC Send Message)
0039       //
0040       VA frm_cHeaderDirty = "0"
0041       //
0042       // SAR 8299676 - Send the Error message to the parent if we have header
0043       // processed with Error.
0044    Else
0045       SI nMessageType = VA frm_nMSG_HEADER_PROCESSED_ERR
0046       Press Button(FC Send Message)
0047    End If
0048    Press Button(FC Create Form Exits)
0049    //
0050    // *************************************************************
0051    // GSI Begin
0052    // GSI/Scott Beebe  1-8-14
0053    // *************************************************************
0054    // The order header form is called in an "Auto Close" code.  The header form
0055    // does some work to update detail lines for fields marked in P40HDR.  To get
0056    // the "deliver to" value to update all detail lines to the new value we call
0057    // the header form, let it do its work, and then close.  This allows us to
0058    // take advantage of the STD functionality that will update detail lines with
0059    // the header changes.
0060    //
0061    // The STD functionality seems to only work when revising an existing order
0062    // and not on new orders.
0063    //
0064    Call( App:P5542101 , Form:W5542101E , Ver: SI szViewVersion )
           SI szSessionKey -> FI szSessionKey
           "1" -> FI cAutoClose
0065    //
0066    // After calling the header form we need to force a refresh of the detail
0067    // lines in the grid.
0068    SI nMessageType = VA frm_nMSG_HDR_TO_DETAIL_UPDATE
0069    Press Button(FC Send Message)
0070    // *************************************************************
0071    // GSI End
0072    // *************************************************************
0073 End If


 =======================================================================
     CONTROL:  BUTTON Initialize Header [HIDDEN]
     EVENT:  Button Clicked
-----------------------------------------------------------------------
     OPT: Using Defaults
0001 Set Control Focus(FC Long Address Number Sold To)
0002 //
0003 // Initialize the previous field value variables
0004 //
0005 Press Button(FC Set Previous Field Values)
0006 FC Previous Order Number = SI mnPreviousOrderNumber
0007 FC Previous Order Type = SI szPreviousOrderType
0008 FC Previous Order Company = SI szPreviousOrderCompany
0009 //
0010 S421007A Initialize Sales Order Header
        SI szSessionKey -> BF szSessionKey
        SI szViewVersion -> BF szViewVersion
        VA frm_cAdvancedPricingYN <- BF cAdvancedPricing
        VA frm_cTemplatesEnabled <- BF cTemplateProcessing
        VA frm_cOrderPromisingFlag <- BF cOrderPromisingFlag
        VA frm_cOrderMode <- BF cOrderMode
        VA frm_cAddressBookRevisions <- BF cAddressBookRevisions
        VA frm_cPrePaymentFlag <- BF cPrePaymentFlag
        VA frm_cOrderFulfillmentEnabled <- BF cOrderFulfillmentEnabled
        VA frm_cShoppingCartModeEnabled <- BF cShoppingCartModeEnabled
        VA frm_cApparelEnabled <- BF cApparelEnabled
        VA frm_cTranferOrderEnabled_EV01 <- BF cTransferOrderEnabled
        VA frm_cStoreAndForwardEnabled <- BF cStoreAndForwardEnabled
        VA frm_cInterBranchEnabled_EV01 <- BF cInterBranchEnabled
0011 //
0012 // cHeaderDirty is initialized to 0 when header was added/edited first
0013 If SI cHeaderFirst is equal to "1" And VA frm_cOrderMode is not equal to "C"
0014    VA frm_cHeaderDirty = "0"
0015 Else
0016    VA frm_cHeaderDirty = "1"
0017 End If
0018 //
0019 If VA frm_cOrderMode is not equal to "U"
0020    Press Button(FC Create Form Exits)
0021 End If
0022 //
0023 // ****************************************************************
0024 // GSI Begin
0025 // GSI/Sbeebe  11-25-13
0026 // ****************************************************************
0027 // Retreive the current custom fields from the F4201
0028 F4201.Fetch Single
        SI szOrderCompany =  TK Order Company (Order Number)
        SI mnOrderNo =  TK Document (Order No, Invoice, etc.)
        SI szOrderType =  TK Order Type
        FC Paid By <- TK Address Number - Paid By
        FC Deliver To <- TK Address Number - Deliver To
0029 //
0030 // Get paid to name
0031 F0101.Fetch Single
        FC Paid By =  TK Address Number
        FC PAID BY <- TK Name - Alpha
0032 // ****************************************************************
0033 // GSI End
0034 // ****************************************************************


Subform Function
=======================================================================
     CONTROL:  FUNCTION AbcSaveCustomDataToCache
     EVENT:  Function Body
-----------------------------------------------------------------------
     OPT: Using Defaults
0001 // GSI/Scott Beebe  11-25-13
0002 // Save the custom data on the form to cache
0003 //
0004 VA frm_KeyName_DL010 = "DelToPaidTo"
0005 VA frm_Value1_DL010 = [FC Deliver To]
0006 VA frm_Value2_DL010 = [FC Paid By]
0007 Load Keyed Cache
        SI szSessionKey -> BF szSessionKey
        VA frm_KeyName_DL010 -> BF szKey1
        VA frm_Value1_DL010 -> BF szValue1
        VA frm_Value2_DL010 -> BF szValue2


